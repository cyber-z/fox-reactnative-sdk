apply plugin: 'com.android.library'
apply plugin: 'maven'

group = 'co.cyberz.fox.support'
version = '1.0.0'
def artifact = 'track-reactnative'
description = 'F.O.X Android SDK React Native Plugin'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        google()
    }
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.1"
    publishNonDefault false

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    maven {
        url "https://github.com/cyber-z/public-fox-android-sdk/raw/master/mavenRepo"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    provided "com.facebook.react:react-native:+"  // From node_modules
    provided 'co.cyberz.fox:track-core:4.4.1'
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: "file:${projectDir}/mavenRepo")
        pom.project {
            licenses {
                license {
                    name "Copyright Â© CyberZ, inc. All Rights Reserved."
                }
            }
        }
        pom.artifactId = "${artifact}"
    }
}

task removeJar(type:Delete) {
    delete fileTree(dir:'build/outputs/jar/', include:("*"))
}


task copyJar(type:Copy, dependsOn:removeJar) {
    from('build/intermediates/bundles/release/')
    into('build/outputs/jar/')
    include('classes.jar')
    rename('classes.jar', "FOX_Android_SDK_Support_ReactNative_${version}.jar")
}

task assembleReleaseJar(dependsOn:copyJar) << {
    println("assemble jar : " + copyJar.destinationDir)
}